mod core.io;

extern (C) putchar (const c : char);
extern (C) printf (const c : p!char, ...);

def print (c : char) -> void
    putchar (c);

def print (const s : string) -> void {
    let i = 0U;
    while i < s.len {
        print (s [i]);
        i = i + 1U;
    }
}

def print (i : i64) ->  void {
    printf ("%ld".ptr, i);
}

def print (i : i32) ->  void {
    printf ("%d".ptr, i);
}

def print (i : i16) ->  void {
    printf ("%hd".ptr, i);
}

def print (i : i8) ->  void {
    printf ("%hhx".ptr, i);
}

def print (i : u64) ->  void {
    printf ("%lu".ptr, i);
}

def print (i : p!void) ->  void {
    printf ("%lu".ptr, i);
}

def print (T) (i : p!T) ->  void {
    printf ("%lu".ptr, i);
}

def print (i : u32) ->  void {
    printf ("%u".ptr, i);
}

def print (i : u16) ->  void {
    printf ("%hu".ptr, i);
}

def print (i : const u8) -> void {
    printf ("%hhx".ptr, i);
}

def print (a : f32, prec : i64) {
    printf ("%.*lf".ptr, prec, a);
}

def print (a : f64, prec : i64) {
    printf ("%.*lf".ptr, prec, a);
}

def print (f : f32) {
    printf ("%lf".ptr, f);
}

def print (f : f64) {
    printf ("%lf".ptr, f);
}

def print (b : bool) {
    if (b) print ("true");
    else print ("false");
}

def print (T of [U], const U) (a : const T) {
    let i = 0U;
    print ('[');
    while i < a.len {
        print (a [i]);
        if i < a.len - 1U
            print (", ");
        i = i + 1U;
    }
    print (']');
}

def print (F, T...) (a : const F, b : const T) {
    print (a);
    print (b.expand);    
}

def println (F, T...) (s : const F, i : const T) {
    print (s);
    print (i.expand);
    print ('\n');
}

def println (T) (const i : T) {
    print (i);
    print ('\n');
}

def println if (is (T : tuple)) (T) (const i : T) {
    print (i);
    print ('\n');
}

def print (T) (const tu : t!(const T)) {
    print (tu.0);
}

def print if (is (T : tuple)) (T) (const tu : T) {
    print ('(');
    printTuple (tu);
    print (')');
}

private def printTuple (T) (const tu : t!(const T)) {
    print (tu.0);
}

private def printTuple if (is (T : tuple)) (T) (const tu : T) {
    imut if (tu::arity >= 2) {
        let (a, b...) = tu;
        print (a);
        print (", ");
        printTuple (b);
    }
}

def print if (is (T : struct)) (T) (const a : T) {
    print (a::typeid);
    print ('(');
    printTuple (a.tupleof);
    print (')');
}

def print (T, N : u64) (const a : [const T ; N]) {
    print ('[');
    for it in 0U .. a.len {
        print (a [it]);
        if it < a.len - 1U
            print (", ");
    }
    print (']');
}


