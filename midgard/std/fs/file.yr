mod std::fs::file;

import core::object, core::typeinfo, core::exception;
import core::duplication, core::array, core::dispose;

import std::conv;
import std::io;
import std::fs::path;
import std::fs::errors;
import std::fs;

/**
 * This class is a reference to a file in the file system
 * @warning: files are disposable
 * @example: 
 * ===========
 * with dmut file = File::create ("foo.txt", write-> true) {
 *     file:.write ("Hello world !!\\n"s8);
 *  } catch {
 *      err : &FsError => {
 *          println (err);
 *      }
 *  }
 * ===========
 */
pub class @final File {

    prv let dmut _handle : &(void) = null;
    
    prv let mut _filename : [c8];

    prv let _read : bool;

    prv let _write : bool;

    prv let _append : bool;    
    
    /**
     * Open a file in the file system
     * @throws: FsError, if the file failed to be created or opened
     * @info: if the file already existed, it will be erased
     */
    pub self create (path : &Path, read : bool = true, write : bool = false, append : bool = false)
        with _filename = path.toStr (),
             _read = read,
             _write = write && !append,
             _append = append
        throws &FsError
    {
        std::fs::createFile (path);
        self:.open ();
    }
    
    /**
     * Open a file in the file system
     * @throws: FsError, if the file does not exists
     */
    pub self open (path : &Path, read : bool = true, write : bool = false, append : bool = false)
        with _filename = path.toStr (),
             _read = read,
             _write = write && !append,
             _append = append
        throws &FsError
    {
        self:.open ();
    }

    /**
     * Open the file 
     */
    prv def open (mut self)
        throws &FsError
    {
        let mode = self.createMode ();
        self._handle = etc::c::files::fopen ((self._filename.toStringZ ()), mode.ptr);
        if (self._handle is null) {
            throw FsError::new (errno ().to!(FsErrorCode) (), self._filename);   
        }
    }

    /**
     * Read all the char contained in the file from the current cursor position
     * @example: 
     * ==============
     * let dmut fs = File::create ("foo.txt", write-> true);
     * fs:.write ("Hello World !"s8);
     * fs:.seek (0u64);
     * 
     * let res = fs:.readAll ();
     * assert (res == "Hello World !"s8);
     * ==============
     * @throws: 
     *   - FsError: if the file is not opened, or not readable
     */
    pub def readAll (mut self) -> dmut [c8]
        throws &FsError
    {
        if (self._handle is null) {
            throw FsError::new (FsErrorCode::FILE_CLOSED, self._filename);
        } else if (!self._read) {
            throw FsError::new (FsErrorCode::NOT_READABLE, self._filename);
        }
        
        if (!etc::c::files::feof (self._handle)) {
            let cursor = etc::c::files::ftell (self._handle);
            etc::c::files::fseek (self._handle, 0u32, SeekWhence::SEEK_END);
            let len = etc::c::files::ftell (self._handle) - cursor;
            etc::c::files::fseek (self._handle, cursor, SeekWhence::SEEK_SET);
            
            if (len != 0u32) {
                let dmut res = core::duplication::allocArray!(c8) (cast!(u64) (len));
                etc::c::files::fread (alias cast!(&void) (res.ptr), cast!(u32) (sizeof (c8)), cast!(u32) (len), alias self._handle);
                
                return alias res
            }
        }
        
        []
    }

    /**
     * Read the content of a file until the delimiter text if found or EOF
     * @example: 
     * ==============
     * let dmut fs = File::create ("foo.txt", write-> true);
     * fs:.write ("Hello World !"s8);
     * fs:.seek (0u64);
     * 
     * let res = fs:.readUntil (" !"s8);
     * assert (res == "Hello World"s8);
     * ==============
     * @throws: 
     *   - FsError: if the file is not opened, or not readable
     */
    pub def readUntil (mut self, delim : [c8])-> dmut [c8]
        throws &FsError
    {
        if (self._handle is null) {
            throw FsError::new (FsErrorCode::FILE_CLOSED, self._filename);
        } else if (!self._read) {
            throw FsError::new (FsErrorCode::NOT_READABLE, self._filename);
        }
        
        import std::collection::vec;
        let dmut res = Vec!(mut c8)::new ();
        let mut current = 0u64;
        
        while (!etc::c::files::feof (self._handle) && current < delim.len) {
            let u : u32 = etc::c::files::fgetc (alias self._handle);
            if (u >= u8::max) break {} // feof is not triggered before trying to read it, so this u can be eof == 256

            let c = cast!(c8) (cast!(u8) (u));
            res:.push (c);
            {
                if (delim [current] == c) { current += 1u64; }
                else { current = 0u64; }
            } catch {
                _ : &OutOfArray => { } // Can't happen, current always < delim.len
            }
        }
        
        let mut aux : [mut c8] = core::duplication::allocArray!c8 (res.len ());
        core::duplication::memCopy!c8 (res[], alias aux);
        
        return alias aux;
    }

    /**
     * Read the content of a file until the delimiter text if found or EOF
     * @params: 
     *    - delim: the delimiter 
     * @example: 
     * ==============
     * let dmut fs = File::create ("foo.txt", write-> true);
     * fs:.write ("Hello World !"s8);
     * fs:.seek (0u32);
     * 
     * let res = fs:.readUntil (" !");
     * assert (res == "Hello World"s8);
     * ==============
     * @throws: 
     *   - FsError: if the file is not opened, or not readable
     */
    pub def readUntil (mut self, delim : [c32])-> dmut [c8]
        throws &FsError
    {
        if (self._handle is null) {
            throw FsError::new (FsErrorCode::FILE_CLOSED, self._filename);
        } else if (!self._read) {
            throw FsError::new (FsErrorCode::NOT_READABLE, self._filename);
        }
        
        return self:.readUntil (to![c8] (delim));
    }

    
    /**
     * Write some text to a file
     * @throws: 
     *   - FsError: if the file is not opened, or not writable
     */
    pub def write (mut self, text : [c8]) -> void
        throws &FsError
    {
        if (self._handle is null) {
            throw FsError::new (FsErrorCode::FILE_CLOSED, self._filename);
        } else if (!self._write && !self._append) {
            throw FsError::new (FsErrorCode::NOT_WRITABLE, self._filename);
        }
        
        etc::c::files::fwrite (cast!(&void) (text.ptr), cast!(u32) (sizeof (c8)), cast!(u32) (text.len), alias self._handle);        
    }
    


    /**
     * Read all the char contained in the file from the current cursor position
     * @example: 
     * ==============
     * let dmut fs = File::create ("foo.txt", write-> true);
     * fs:.write ("Hello World !"s8);
     * fs:.seek (0u32);
     * 
     * let res : [u8] = fs:.readBytesAll ();
     * ==============
     * @throws: 
     *   - FsError: if the file is not opened, or not readable
     */
    pub def readBytesAll (mut self) -> dmut [u8]
        throws &FsError
    {
        if (self._handle is null) {
            throw FsError::new (FsErrorCode::FILE_CLOSED, self._filename);
        } else if (!self._read) {
            throw FsError::new (FsErrorCode::NOT_READABLE, self._filename);
        }
        
        if (!etc::c::files::feof (self._handle)) {
            let cursor = etc::c::files::ftell (self._handle);
            etc::c::files::fseek (self._handle, 0u32, SeekWhence::SEEK_END);
            let len = etc::c::files::ftell (self._handle) - cursor;
            etc::c::files::fseek (self._handle, cursor, SeekWhence::SEEK_SET);
            
            if (len != 0u32) {
                let dmut res = core::duplication::allocArray!(u8) (cast!(u64) (len));
                etc::c::files::fread (alias cast!(&void) (res.ptr), cast!(u32) (sizeof (u8)), cast!(u32) (len), alias self._handle);
                
                return alias res
            }
        }
        
        []
    }

    /**
     * Read the content of a file until the delimiter bytes is found or EOF
     * @example: 
     * ==============
     * let dmut fs = File::create ("foo.txt", write-> true);
     * fs:.write ("Hello World !"s8);
     * fs:.seek (0u32);
     * 
     * let res : [u8] = fs:.readBytesUntil ([cast!(u8)('\n'c8)]);
     * ==============
     * @throws: 
     *   - FsError: if the file is not opened, are not readable
     */
    pub def readBytesUntil (mut self, delim : [u8])-> dmut [u8]
        throws &FsError
    {
        if (self._handle is null) {
            throw FsError::new (FsErrorCode::FILE_CLOSED, self._filename);
        } else if (!self._read) {
            throw FsError::new (FsErrorCode::NOT_READABLE, self._filename);
        }
        
        import std::collection::vec;
        let dmut res = Vec!(u8)::new ();
        let mut current = 0u64;
        
        while (!etc::c::files::feof (self._handle) && current < delim.len) {
            let u : u32 = etc::c::files::fgetc (alias self._handle);
            if (u >= u8::max) break {} // feof is not triggered before trying to read it, so this u can be eof == 256

            let c = cast!(u8) (u);
            res:.push (c);
            {
                if (delim [current] == c) { current += 1u64; }
                else { current = 0u64; }
            } catch {
                _ : &OutOfArray => { } // Can't happen, current always < delim.len
            }
        }
        
        let mut aux : [mut u8] = core::duplication::allocArray!u8 (res.len ());
        core::duplication::memCopy!u8 (res[], alias aux);
        
        return alias aux;
    }
    
    /**
     * Write some bytes to a file
     * @throws: 
     *   - FsError: if the file is not opened, are not writable
     */
    pub def writeBytes (mut self, text : [u8]) -> void
        throws &FsError
    {
        if (self._handle is null) {
            throw FsError::new (FsErrorCode::FILE_CLOSED, self._filename);
        } else if (!self._write && !self._append) {
            throw FsError::new (FsErrorCode::NOT_WRITABLE, self._filename);
        }
        
        etc::c::files::fwrite (cast!(&void) (text.ptr), cast!(u32) (sizeof (u8)), cast!(u32) (text.len), alias self._handle);        
    }
    
    
    /**
     * Move the position of the cursor of the file
     * @info: the cursor position is relative to the beginning of the file
     * @throws: 
     *   - FsError: if the file is not opened
     */
    pub def seek (mut self, cursor : u32)-> void
        throws &FsError
    {
        if (self._handle is null) {
            throw FsError::new (FsErrorCode::FILE_CLOSED, self._filename);
        }
        etc::c::files::fseek (self._handle, cursor, SeekWhence::SEEK_SET);
    }

    /**
     * Tell the position of the cursor in the file relative to the beginning of the file
     * @throws: 
     *   - FsError: if the file is not opened
     */
    pub def tell (self)-> u32
        throws &FsError
    {
        if (self._handle is null) {
            throw FsError::new (FsErrorCode::FILE_CLOSED, self._filename);
        }
        etc::c::files::ftell (self._handle)
    }

    /**
     * @returns: the len of the file (from beginning to end)
     */
    pub def len (self)-> u32
        throws &FsError
    {
        let cursor = self.tell ();
        fseek (self._handle, 0u32, SeekWhence::SEEK_END);
        let len = self.tell ();
        fseek (self._handle, cursor, SeekWhence::SEEK_SET);
        len
    }
    
    /**
     * Close the file
     * @info: if the file was not open, this method does nothing
     */
    pub def close (mut self) {
        if (self._handle !is null) {
            fclose (alias self._handle);
            self._handle = null;
        }
    }

    impl core::dispose::Disposable {
        
        /**
         * Close the file
         * @info: if the file was not open, this method does nothing
         */
        pub over dispose (mut self) -> void {
            self:.close ();
        }
    }

    /**
     * Create the mode of reading 
     */
    prv def createMode (self)-> [c8] {
        if (self._write) return "r+"s8;
        if (self._read && self._append) return "a+"s8;
        if (self._append) return "a"s8;
        
        return "r"s8;
    }

    /**
     * Close the file if the user forgot
     * @warning: 
     * =================
     * because we don't know when of even if the destructor will be called, 
     * this must be used as a last resort, it is always better to close the files 
     * =================
     */
    __dtor (mut self) {
        self:.close ();
    }    
}
