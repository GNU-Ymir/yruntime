mod std::tokenizer;

import core::object;
import core::array;
import core::typeinfo;
import core::exception;
import core::dispose;

import std::collection::map;
import std::collection::vec;
import std::io, std::stream;

/**
 * A tokenizer is an enhanced string splitter, that splits strings using tokens instead of just chars
 * Tokenizer are really usefull for grammar visitor, and can be associated with Lexers pretty easily
 * @example: 
 * ============
 * // using a tokenizer, tokens can be multiple letter long, and there can be collision between tokens
 * // For example, the token '=' and '=>' won't be a problem for the tokenizer
 * let dmut tzer = Tokenizer::new (tokens-> ["(", ")", "=>", ",", " ", "="]);
 * let str = "(x, y) => x = y";
 * let lst = tzer.tokenize (str);
 * 
 * // the tokenized str, contains the token '=>' and '=', that are correctly rendering in the splitted array 
 * assert (lst == ["(", "x", ",", " ", "y", ")", " ", "=>", " ", "x", " ", "=", " ", "y"]); 
 * ============
 */
pub class if (is!T {U of c8} || is!T {U of c32}) @final Tokenizer {T} {

    prv let dmut _heads = HashMap!{T, &internal::Node!{T}}::new ();

    /**
     * Create a new tokenizer, with a set of tokens
     * @params: 
     *   - tokens: the list of token that will split the string
     * @example: 
     * ============
     * let dmut tzer = Tokenizer::new (tokens-> ["(", ")", "=>", ":", "<", ">", ",", " "]);
     * let str = "(x, y) => x > y";
     * let lst = tzer.tokenize (str);
     * assert (lst == ["(", "x", ",", " ", "y", ")", " ", "=>", " ", "x", " ", ">", " ", "y"]); 
     * ============
     */
    pub self (tokens: [[T]] = []) {
        for i in tokens {
            self:.insert (i);
        }
    }

    /**
     * Insert a new token in the tokenizer
     * @params: 
     *    - token: the token to insert
     * @example: 
     * ================
     * let dmut tzer = Tokenizer::new ();
     * tzer:.insert ("+");
     * tzer:.insert ("+=");
     * tzer:.insert (" ");
     * let lst = tzer.tokenize ("x += y");
     * assert (lst == ["x", " ", "+=", " ", "y"]);
     * ================
     */
    pub def insert (mut self, token : [T]) {
        if (token.len != 0us) {
            {
                let fnd = (self._heads [token[0]])?;
                match fnd {
                    Ok (x:_) => {
                        (alias self._heads) [token[0]] = x.insert (token[1us..$]);                    
                    }
                    _ => {
                        (alias self._heads) [token[0]] = internal::Node!{T}::new (token[0]).insert (token [1us..$]);
                    }
                }
            } catch {
                _ : &OutOfArray => { } // impossible
            }
        }
    }

    /**
     * @returns: the length of the next token inside the str
     * @example: 
     * ============
     * let dmut tzer = Tokenizer::new (["+", " "]);
     * let mut str = "fst + scd";
     * let mut len = tzer.next (str);
     * assert (len == 3us); // "fst"
     * 
     * str = str [len .. $];     
     * len = tzer.next (str);
     * assert (len == 1us); // " "
     *
     * str = str [len .. $];     
     * len = tzer.next (str);
     * assert (len == 1us); // "+"
     *
     * str = str [len .. $];     
     * len = tzer.next (str);
     * assert (len == 1us); // " "
     *
     * str = str [len .. $];     
     * len = tzer.next (str);
     * assert (len == 3us); // "scd" 
     *
     * str = str [len .. $];     
     * len = tzer.next (str);
     * assert (len == 0us); 
     * ============
     */
    pub def next (self, str : [T])-> usize {
        for i in 0us .. str.len {
            let fnd = (self._heads [str [i]])?;
            {
                match fnd { 
                    Ok (x:_) => { // a possible token at index == i
                        let len = x.len (str [i + 1us .. $]); // get the length of the token
                        // if the len is 0, then it is not really a token, it just start like one
                        if (len != 0us) {
                            if (i == 0us) { 
                                return len; // it is totally a token, we return its length
                            } else {
                                // it is a token, but there is something before it, so we return the len of the thing before it
                                return i; 
                            }
                        }
                        // it was not a token, just started like one, we continue
                    }
                }
            } catch {
                _ : &OutOfArray=>  {} // impossible
            }
        }
        
        // No token in the str, return the len of the str
        return str.len;
    }

    /**
     * Split the string in a list of token, according to the token registered in the tokenizer
     * @example: 
     * =============== 
     * let dmut tzer = Tokenizer::new (tokens-> ["+=", "+", " "]);     * 
     * 
     * let lst = tzer.tokenize ("x += y");
     * assert (lst == ["x", " ", "+=", " ", "y"]);
     * ===============
     */
    pub def tokenize (self, str : [T])-> [[T]] {
        let dmut res = Vec!([T])::new ();
        let mut aux = str;
        { 
            while aux.len > 0us {
                let len = self.next (aux);
                res:.push (aux [0us..len]);
                aux = aux [len .. $];
            }
        } catch {
            _: &OutOfArray=> {}
        }
        
        res:.fit ();
        return res [];
    }

    impl Streamable ;
   
    
}

mod internal {

    /**
     * A node of a tokenizer, that stores information about tokens
     */
    pub class @final Node {T} {

        // The current value of the node
        let _key : T; 

        // Can terminate a token? or is part of bigger tokens
        let _isToken : bool = false;

        // The list of possible continuation of the token
        let _heads : &HashMap!{T, &Node!{T}};

        /**
         * Construct a new Token node
         * @params: 
         *   - key: the value of the node
         *   - isToken: can terminate a token 
         *   - heads: the list of possible continuation
         */
        pub self (key : T, isToken : bool = false, heads : &HashMap!{T, &Node!{T}} = {HashMap!{T, &Node!{T}}::new ()}) with _key = key, _isToken = isToken, _heads = heads
        {}
        
        /**
         * Insert sub tokens accepted tokens
         * @params: 
         *     - str: the rest to read to create a valid token
         * @example: 
         * ==============
         * // let say that "[+]" is a token, but "[" is not, nor is "[+"
         * let mut node = Node::new ('['); 
         * node = node.insert ("+]"); 
         * println (node); // [:false, +:false, ]:true 
         * // In that configuration the only token that will be accepted is "[+]"
         * assert (node.len ("[+]") == 3); // accepted
         * assert (node.len ("[") == 0us); // not accepted
         * assert (node.len ("[+") == 0us); // not accepted
         * 
         * // Now we want to accept "[-]"
         * node = node.insert ("-]");
         * // and simply "["
         * node = node.insert (""); 
         * println (node); // [:true, 
         *                 //     +:false, ]:true 
         *                 //     -: false, ]:true
         * 
         * assert (node.len ("[+]") == 3); // still accepted
         * assert (node.len ("[") == 1us); // accepted this time
         * assert (node.len ("[+") == 1us); // accept only the '['
         * assert (node.len ("[-]") == 3); // accepted
         * assert (node.len ("[-") == 1us); // accept only the '['
         * ==============
         */
        pub def insert (self, str : [T]) -> &Node!{T} {
            if (str.len == 0us) {
                return Node::new (self._key, isToken-> true, heads-> self._heads)
            }

            let dmut retDict = HashMap!{T, &Node!{T}}::new ();
            for i, j in self._heads {
                retDict:.insert (i, j);                    
            }

            let fnd = retDict[str [0]]?
            {
                match fnd {
                    Ok (x:_) => {
                        retDict:.insert (str [0], x.insert (str [1us..$]));
                    }
                    _ => {                    
                        retDict:.insert (str [0], Node::new (str [0]).insert (str [1us..$]));
                    }
                }
            } catch {
                _: &OutOfArray => {}
            }
            
            return Node::new (self._key, isToken-> self._isToken, heads-> retDict);
        }

            

        /**
         * @returns: the length of the token at the beginning of the string content
         * @example: 
         * =================
         * let mut node = Node::new ('+', isToken-> true);
         * node = node.insert ("=");
         * // Our grammar accept the tokens, "+" and "+="
         * assert (node.len ("+") == 1us); // "+" are accepted
         * assert (node.len ("+=") == 2us); // "+=" are accepted
         * assert (node.len (" +=") == 0us); // " +=" are not accepted
         * assert (node.len ("+ and some rest") == 1us); // " +" are accepted
         * =================
         */
        pub def len (self, content : [T])-> usize {
            if (content.len == 0us) {
                if (self._isToken) return 1us;
                else return 0us;
            }

            let fnd = (self._heads [content [0]])?
            match fnd {
                Ok (x:_) => {
                    let sub_len = x.len (content [1us..$]);
                    if (sub_len != 0us || self._isToken) return 1us + sub_len;
                    else return 0us;                
                } catch {
                    _: &OutOfArray => {} // impossible
                }
            }

            if (self._isToken) return 1us;
            return 0us;
        }

        /**
         * @returns: the key of the node
         */
        def key (self) -> T {
            self._key
        }

        impl Streamable {
            /**
             * For debug purposes, tokenizer Nodes are printable
             */
            pub over toStream (self, dmut stream : &StringStream) {
                self.innerPrint (0, alias stream);
            }
        }

        /**
         * Debug print of the node on stdout
         */
        prv def innerPrint (self, i : i32, dmut stream : &StringStream) {
            for _ in 0 .. i { stream:.write ("\t"s8); }            
            stream:.write ('['c8):.write (self._key):.write (']'c8):.write (self._isToken);
            for _, j in self._heads {                
                j.innerPrint (i + 1, alias stream);                
            }            
        }
        
    }

    
}



