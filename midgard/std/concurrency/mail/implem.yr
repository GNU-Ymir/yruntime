in implem;

use std::stream;

/**
 * Atomic linked list storing
 * */
@abstract
pub class IMailBox {T, mutable : bool} {

    cte if mutable {
        // The list of mails
        let dmut _mails : [T] = [];
    } else {
        let mut _mails : [mut T] = [];
    }

    /**
     * Create a new empty mailbox
     * */
    pub self () {}


    cte if !mutable {
        /**
         * Post a message in the mailbox
         * @params:
         *   - value: the value to post
         * */
        pub fn send (mut self, value : T) {
            atomic self {
                self._mails ~= [value];
            }
        }

        /**
         * @returns: the first value found in the box if any
         * */
        pub fn receive (mut self)-> (T)? {
            atomic self {
                if (self._mails.len == 0us) return none;
                let fr = self._mails [0];
                self._mails = alias self._mails [1 .. $];

                return fr?;
            }
        }

        /**
         * @returns: all the messages in the mailbox
         * */
        pub fn opIndex (mut self)-> [T] {
            atomic self {
                let mut result = self._mails;
                self._mails = [];

                return result;
            }
        }
        
    } else {
        /**
         * Post a message in the mailbox
         * @params:
         *   - value: the value to post
         * */
        pub fn send (mut self, dmut value : T) {
            atomic self {
                self._mails ~= [alias value];
            }
        }

        /**
         * @returns: the first value found in the box if any
         * */
        pub fn receive (mut self)-> dmut (T)? {
            atomic self {
                if (self._mails.len == 0us) return none;

                let dmut fr = alias self._mails [0];
                self._mails = alias self._mails [1 .. $];

                (alias fr)?
            }
        }

        /**
         * @returns: all the messages in the mailbox
         * */
        pub fn opIndex (mut self)-> dmut [T] {
            atomic self {
                let dmut result = alias self._mails;
                self._mails = [];

                return alias result;
            }
        }

    }


    /**
     * Clear everything that can be found in the mailbox
     * */
    pub fn clear (mut self) {
        atomic self {
            self._mails = [];
        }
    }

    /**
     * @returns: the number of element in the box
     * */
    @field
    pub fn len (self)-> usize {
        self._mails.len
    }

    impl Streamable {
        pub over toStream (self, dmut stream : &StringStream) {
            stream:.write ("MailBox {");
            atomic self {
                for i, m in self._mails {
                    cte if __pragma!compile ({ stream:.write (m); }) {
                        if i != 0 { stream:.write (", "); }
                        stream:.write (m);
                    } else {
                        i;
                        m;
                    }
                }
            }
            stream:.write ("}");
        }
    }

}
