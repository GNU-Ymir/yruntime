mod std::io;

extern (C) def putchar (c : c8);
extern (C) def _yrt_putwchar (c : c32);
extern (C) def _yrt_getwchar ()-> c32;

extern (C) def printf (c : &c8, ...);
extern (C) def scanf (c : &c8, ...);

/**
 * This trait is to be implemented by class
 * When a class implement it, we can call the function print on it
 * Example : 
 * ====================
class X {
// ...
impl Printable;
}

let a = X::new ();
println ("Test on : ", a); 
 * ====================
 */
pub trait Printable {
    pub def print (self)-> void {
        print (typeof (self)::typeid);
    }
}

pub {
    /**
     * Print a char of size 8 in stdout
     * Params : 
     * - c, the char to print
     */
    def print (c : c8) -> void
        putchar (c)

    /**
     * Print a char of size 32 in stdout
     * Params : 
     * - c, the char to print
     */
    def print (c : c32) -> void
        _yrt_putwchar (c)

    /**
     * Print an array of c32 in stdout
     * Params : 
     * - s, the string to print
     */
    def print (s : [c32]) -> void {
        let mut i = 0_u64;
        while i < s.len {
            print (s [i]);
            i = i + 1_u64;
        }
    }

    /**
     * Print an array of c8 in stdout
     * Params : 
     * - s, the string to print
     */
    def print (s : [c8]) -> void {
        let mut i = 0_u64;
        while i < s.len {
            print (s [i]);
            i = i + 1_u64;
        }
    }

    /**
     * Print a isize to stdout
     * Params:
     * i, the int to print
     */
    def print (i : isize) -> void
        printf ((alias "%ld"s8).ptr, i)

    /**
     * Print a usize to stdout
     * Params:
     * i, the int to print
     */
    def print (i : usize) -> void
        printf ((alias "%ld"s8).ptr, i)
    
    /**
     * Print a i64 to stdout
     * Params:
     * i, the int to print
     */
    def print (i : i64) -> void
        printf ((alias "%ld"s8).ptr, i)

    /**
     * Print a i32 to stdout
     * Params:
     * i, the int to print
     */
    def print (i : i32) -> void
        printf ((alias "%d"s8).ptr, i)

    /**
     * Print a i16 to stdout
     * Params:
     * i, the int to print
     */
    def print (i : i16) -> void
        printf ((alias "%hd"s8).ptr, i)

    /**
     * Print a i8 to stdout
     * Params:
     * i, the int to print
     */
    def print (i : i8) -> void
        printf ((alias "%hhx"s8).ptr, i)

    /**
     * Print a u64 to stdout
     * Params:
     * i, the int to print
     */
    def print (i : u64) -> void
        printf ((alias "%lu"s8).ptr, i)
        
    /**
     * Print a u32 to stdout
     * Params:
     * i, the int to print
     */
    def print (i : u32) -> void
        printf ((alias "%u"s8).ptr, i)

    /**
     * Print a u16 to stdout
     * Params:
     * i, the int to print
     */
    def print (i : u16) -> void
        printf ((alias "%hu"s8).ptr, i)

    /** 
     * Print a u8 to stdout
     * Params:
     * i, the int to print
     */
    def print (i : u8) -> void
        printf ((alias "%hhx"s8).ptr, i)


    /**
     * Print a float value of size 64 to stdout
     * Params:
     * - f, the float to print
     * - prec, the precision of the print (number of decimal)
     */
    def print (f : f64, prec : i32) -> void
        printf ((alias "%.*lf"s8).ptr, prec, f)
        
    /**
     * Print a float value of size 32 to stdout
     * Params:
     * - f, the float to print
     * - prec, the precision of the print (number of decimal)
     */
    def print (f : f32, prec : i32) -> void
        printf ((alias "%.*f"s8).ptr, prec, f)
        

    /**
     * Print a float value of size 64 to stdout
     * Params:
     * - f, the float to print
     */
    def print (f : f64) -> void {
        print (f, 5);
    }
        
    /**
     * Print a float value of size 32 to stdout
     * Params:
     * - f, the float to print
     */
    def print (f : f32) -> void {
        print (f, 5);
    }
        

    /**
     * Print a bool value to stdout
     * Params:
     * - b, the bool to print
     */
    def print (b : bool) -> void {
        if (b) 
            printf ((alias "true"s8).ptr)
        else
            printf ((alias "false"s8).ptr)
    }

    /**
     * Print an array of type U to stdout
     * Params:
     * - a, an array to print
     * Templates : 
     * - T, an array type
     * - U, any 
     */
    def print (T of [U], U) (a : T) -> void {
        let mut i = 0_u64;
        print ('['c8);
        while i < a.len {
            if i != 0_u64
                printf ((alias ", "s8).ptr);
            
            print (a [i]);
            i = i + 1_u64;
        }
        print (']'c8);
    }

    /**
     * Print a static array of type U to stdout
     * Params : 
     * - a, an array to print
     * Templates : 
     * - T, an static array type 
     * - U, any
     * - N, the static size 
     */
    def print (T of [U;N], U, N : usize) (a : T) -> void {
        print ('['c8);
        for i in 0_u64 .. cast!u64 (N) {
            if i != 0_u64
                printf ((", "s8).ptr);
            print (a [i]);
        }
        print (']'c8);
    }
    
    /**
     * Print multiple elements to stdout
     */
    def print (F, T...) (a : F, b : T) {
        print (a);
        print (expand b);
    }

    /**
     * Print a struct to stdout
     */
    def print (struct T) (a : T) -> void {
        print (T::typeid, "(");
        let mut j = 0;
        for i in a::tupleof {
            if (j != 0) print (", ");
            print (i);
            j += 1;
        }
        print (")");
    }

    def print (T impl Printable) (a : T)-> void {
        a.print ();
    }
    
    /**
     * Print multiple elements to stdout
     * And end with a `'\n'_c8`
     */
    def println (F...) (a : F) {
        print (expand a);
        print ('\n'c8);
    }

    /**
     * Read a i32 from the stdin
     * Returns : a i32
     */
    def read (T of i32) () -> i32 {
        let mut x : i32 = 0;
        scanf ("%d"s8.ptr, &x);
        x
    }

    /**
     * Read a u64 from the stdin
     * Returns : a u64
     */
    def read (T of u64) () -> u64 {
        let mut x : u64 = 0_u64;
        scanf ("%ld"s8.ptr, &x);
        x
    }

    /**
     * Read a c8 from the stdin
     * Returns : a c8
     */
    def read (T of c8) () -> c8 {
        let mut x : c8 = '\u{0}'c8;
        scanf ("%c"s8.ptr, &x);
        x
    }

    /**
     * Read a c8 from the stdin, and return it as a c32
     * Returns : a c32
     */
    def read (T of c32) () -> c32 {
        _yrt_getwchar ()       
    }    

}

extern (C) {
    def fopen  (path : &c8, mode : &c8) -> &i32;
    def fclose (file : &i32) -> i32;
    def fgetc  (file : &i32) -> c8;
    def fgets  (buf : &c8, n : i32, file : &i32) -> i32;
    def feof   (file : &i32)-> i32;

    def _yrt_getwchar_in_file (file : &i32) -> c32;
}

pub trait Readable {
    pub def read (mut self)-> void;
}

/**
 * File open, read, and write
 **/    
pub {    

    enum
    | EOF = -1
     -> CursorState; 
    
    struct
    | handle : &i32
     -> File;

    def open (filePath : [c8], mode : [c8]) -> File {
        File (fopen (filePath.ptr, mode.ptr))
    }

    def close (ref mut file : File) {
        fclose (file.handle);
        file = File (null);
    }

    def isOpen (file : File) -> bool {
        file.handle !is null
    }

    def readc (T of c8) (ref mut file : File) -> c8 {
        fgetc (file.handle)
    }

    def readc (T of c32) (ref mut file : File) -> c32 {
        _yrt_getwchar_in_file (file.handle)
    }
    
    def isEof (file : File) -> bool {
        feof (file.handle) == 1
    }

}
