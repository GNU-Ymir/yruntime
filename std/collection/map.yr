/**
 * This module implements a hash map container.
 * Map container uses the `hash` function of the `std::hash` module to find and insert elements. 
 * <br>
 * This module publically import the mutable version of the `HashMap` from `std::collection::mutable::map`, that can contain mutable values. Keys are always constant.
 *
 * @Authors: Emile Cadorel
 * @License: GPLv3
 * <hr>
 * <br>
 * @example: 
 * ===
 * import std::io; // for println
 * import std::collection::map;
 * 
 * // Create a hash map using the constructor of the class
 * let dmut x = HashMap!{[c32], i32}::new ();
 * x:.insert ("foo", 12);
 * x:.insert ("bar", 32);
 * assert (x ["foo"] == 12 && x ["bar"] == 32);
 *
 * // Or by using the map macro
 * let dmut m = hmap#{"foo" => 78.0f, "bar" => 125.0f};
 * println (m); // {bar=>125.000000, foo=>78.000000}
 * ===
 */

mod std::collection::map;

import core::object;
import core::typeinfo;
import core::duplication;
import core::exception;
import core::array;

import std::io, std::stream;
import std::hash;
import std::conv;

pub import std::collection::mutable::map;

/**
 * The number or allocated node at initialization
 */
prv enum : usize
| DEFAULT_ALLOC_SIZE = 10us
 -> MapConst;


/**
 * Macro used to construct a HashMap with literals
 * @example: 
 * ============
 * let dmut x = hmap#{"foo" => 12, "bar" => 45, "baz" => 89};
 * cte assert (is!{typeof (x)}{T of &HashMap!{[c32], i32}});
 * assert (x["foo"] == 12);
 * ============
 */
pub macro hmap {

    pub self (fst=(key=__expr "=>" val=__expr) rest=("," key=__expr "=>" val=__expr)*) skips (" " | "\n" | "\t" | "\r") {
        let dmut z_#{__index} = HashMap!{typeof (#(fst::key)), typeof (#(fst::val))}::new ();
        z_#{__index}:.insert (#(fst::key), #(fst::val));
        #(for x in rest) {
            z_#{__index}:.insert (#(x::key), #(x::val));
        }
        alias z_#{__index}
    }
    
}


/**
 * A hash map implementation that associated a key to a value
 * The data in the map are unordered
 *
 * @example: 
 * ==========
 * let dmut x = HashMap!{[c32], i32}::new ();
 * x:.insert ("foo", 12);
 * x:.insert ("bar", 32);
 * assert (x ["foo"] == 12 && x ["bar"] == 32);
 * ==========
 */
pub class @final HashMap {K, V} {

    /**
     * Assertion to avoid tones of error printing, if the key is not usable
     */
    cte assert (__pragma!compile ({hash!K;}) || __pragma!compile ({hash(K::init);}), "unusable key type : K = (" ~ K ~ ") must be hashable");
    
    let dmut _data : [dmut &MapNode!{K, V}] = [];
    
    let mut _loaded : usize = 0us;

    let mut _size : usize = 0us;

    let _load_factor = 75us;

    let _min_load_factor = 50us; 

    /// Allocated only once, to optimize memory usage
    let dmut _empty : dmut &MapNode!{K, V} = Empty!{K, V}::new ();

    /**
     * Create a new empty hash map, with a default loaded factor of 0.75 , and a minimum load factor or 0.5
     * Does not allocate memory until first insertion
     * <br> 
     * @info: 
     * load factors are used to detemine the size of the allocation: 
     *    - default load factor, increase the size of the allocation when more than 75% of the allocation is used
     *    - minimum load factor, decrease the size of the allocation when less than 50% of the allocation is used
     * When loads are too low, the linked list at every index will increase in size and find and insert time will increase
     * When loads are too high, the allocation will be too big and memory wasted
     * @params:
     *    - len: the size of the table reserved to avoid reallocation when inserting
     */
    pub self (len : usize = 0us) {
        if (len != 0us) {
            let dmut aux = core::duplication::allocArray!{&MapNode!{K, V}} (len);

            for i in 0us .. aux.len {
                aux [i] = alias self._empty;
            }
            
            self._data = alias aux;
        }
    }
    
    /**
     * Insert a new element inside the map
     * If the key is already found (hash (key)), then the value is updated
     * @params: 
     *    - key: the key 
     *    - val: the value
     * @example: 
     * ==========
     * let dmut x = HashMap!{[c32], i32}::new ();
     * x:.insert ("foo", 12);
     * x:.insert ("bar", 32);
     * assert (x ["foo"] == 12 && x ["bar"] == 32);
     * x:.insert ("test", 24);
     * assert (x ["test"] == 24);
     * ==========
     * @complexity: O (1 + n/k + z) with n the number of possible collision, and k the number of keys, and z the time taken by resizing if the load is higher than the load factor, in average n/k and z are negligeable
     */
    pub def insert (mut self, key : K, val : V) -> void {        
        if (self._data.len == 0us) { self:.grow (); }
        else if ((self._loaded * 100us) / (self._data.len) > self._load_factor) { self:.grow (); }        
        let h = cast!usize (hash (key)); 
        self:.insertFast (h, key, val);
    }

    /**
     * Change the size of the allocation
     * @params:
     *    - len: the size of the allocated array (number of allocated linked list)
     * @complexity: O (n) where n is the number of element in the HashMap
     */
    pub def fit (mut self, len : usize) {
        if (len != 0us) {
            let dmut aux = core::duplication::allocArray!{&MapNode!{K, V}} (len);
            // Filling the data with empty elements, to be usable, allocArray is not safe!?
            for i in 0us .. aux.len {
                aux [i] = alias self._empty;
            }
            
            // Clear the map
            let old = self._data;
            self._data = alias aux;
            self._loaded = 0us;
            self._size = 0us;

            // Add all the old values 
            for i in 0us .. old.len {
                let mut current = old [i];
                loop {
                    match current {
                        x : &MapValue!{K, V} => {
                            self:.insertFast (x.h, x.key, x.val);
                            current = x.next;
                        }
                        _ => {
                            break {}
                        }
                    }
                }
            }            
        }
    } 

    /**
     * Find the element whose key is `key`
     * @params: 
     *    - key: the key to find
     * @returns: The value in an option, or an empty option if there is no element with key `key` in the map
     * @complexity: O (1 + n/k), with n the number of possible collision and k the number of keys, in average n/k are negligeable
     */
    pub def find (self, key : K) -> V?
    {
        if (self._data.len != 0us) {
            let h = cast!usize (hash (key));
            match (self._data [h % self._data.len]) {               
                x : &MapValue!{K, V} => {                  
                    return x.find (h, key);
                } 
            }
        }

        return (V?)::__err__;
    }
    
    /**
     * Search the key in the map, 
     * @returns: true if found, false otherwise
     * @example: 
     * =============
     * let dmut x = HashMap!{[c32], i32}::new ();
     * x:.insert ("foo", 32);
     * assert ("foo" in x);
     * =============
     * @complexity: O (1 + n/k), with n the number of possible collision and k the number of keys, in average n/k are negligeable
     */
    pub def opContains (self, key : K) -> bool {
        if (self._data.len != 0us) {
            let h = cast!usize (hash (key));        
            match (self._data [h % self._data.len]) {
                x : &MapValue!{K, V} => {
                    return x.opContains (h, key);
                }
            }
        }
        false
    }
    
    /**
     * Remove the key in the map
     * If the key was not present, do nothing
     * @params: 
     *    - key: the key to remove
     * @example: 
     * ===========
     * let dmut x = HashMap!{[c32], i32}::new ();
     * x:.insert ("foo", 12);
     * x:.remove ("foo");
     * 
     * match x["foo"]? {
     *      Ok () => assert (false); // 
     * }
     * x:.remove ("bar"); // does nothing
     * ===========
     * @complexity: O (1 + n/k + z) with n the number of possible collision, and k the number of keys, and z the time taken by resizing if the load is lower than the min load factor, in average n/k and z are negligeable
     */
    pub def remove (mut self, key : K) -> void {
        if (self._data.len != 0us) {
            let h = cast!usize (hash (key));
            match ref (self._data [h % self._data.len]) {
                dmut x : &MapValue!{K, V} => {
                    let mut removed = false;
                    
                    self._data [h % self._data.len] = alias x:.remove (h, key, ref removed);
                    match ref (self._data [h % self._data.len]) {
                        _ : &Empty!{K, V} => {
                            self._loaded -= 1us;
                        }
                    }
                    
                    if (removed) {
                        self._size -= 1us;                    
                    }
                }
            }
            
            if ((self._loaded * 100us) / (self._data.len) < self._min_load_factor) { self:.shrink (); }
        }
    }

    /**
     * Find an element using key `k` as key. 
     * @params: 
     *    - k: the key to find
     * @throws:
     *   - &OutOfArray: if the key was not found
     * @returns: the value associated to the key `k`
     * @example: 
     * =============
     * let dmut x = HashMap!{[c32], i32}::new ();
     * x:.insert ("foo", 12);
     * assert (x ["foo"] == 12);
     * match x["bar"] ? {
     *      Ok () => assert (false);
     * }
     * =============
     * @complexity: O (1 + n/k), with n the number of possible collision and k the number of keys, in average n/k are negligeable
     */
    pub def opIndex (self, k : K) -> V
        throws &OutOfArray
    {
        match self.find (k) {
            Ok (v:_) => { return v; }
            _ => throw OutOfArray::new ();            
        }
    }
    
    /**
     * Insert or update the value associated to the key `k`
     * @params: 
     *    - k: the key to insert
     *    - v: the value to associate to the key k
     * 
     * @example: 
     * =============
     * let dmut x = HashMap!{[c32], i32}::new ();
     * (alias x)["foo"] = 12;
     * (alias x)["foo"] = 24;
     * assert (x ["foo"] == 24);
     * =============
     * @complexity: O (1 + n/k + z) with n the number of possible collision, and k the number of keys, and z the time taken by resizing if the load is higher than the load factor, in average n/k and z are negligeable
     */
    pub def opIndexAssign (mut self, k : K, v : V) {
        self:.insert (k, v);
    }

    /**
     * Transform the map into a slice, where each element is a tuple of (key, value)
     * @example:
     * ===
     * let x = hmap#{"foo"=> 12, "bar"=> 134, "baz"=> 89};
     *
     * println (x[]); // ("baz",89), ("bar",134), ("foo",12)
     * ===
     * @complexity: O(n)
     */
    pub def opIndex (self)-> mut [mut (K, V)] {
        let mut res : [mut (K, V)] = core::duplication::allocArray!{(K, V)} (self._size);
        let mut index = 0us;
        for i, j in self {
            res[index] = (i, j);
            index += 1us;
        }

        alias res
    }
    
    /**
     * Remove all the entries contained in the map
     * @example: 
     * =============
     * let dmut x = HashMap!{[c32], i32}::new ();
     * x:.insert ("foo", 34);
     * x:.insert ("bar", 45);
     * x:.clear ();
     * assert (x.isEmpty ());
     * =============
     * @complexity: O (1)
     */
    pub def clear (mut self) {
        self._data = [];
        self._loaded = 0us;
        self._size = 0us;
    }    

    /**
     * The number of element contained in the map
     * @example: 
     * ==========
     * let dmut x = HashMap!{[c32], i32}::new ()
     * x:.insert ("foo", 34);
     * x:.insert ("bar", 45);
     * assert (x.len () == 2us);
     * ==========
     * @complexity: O (1)
     */
    pub def len (self) -> usize {
        self._size
    }

    /**
     * @returns: true if there is no element in the map
     */
    pub def isEmpty (self)-> bool {
        self._size == 0us
    }
    
    /**
     * Iteration over the map can be done with map iterators
     * This iterators implement the ymir interface for iteration with one or two variable (next, get!0, get!1)
     * @example: 
     * ============
     * let dmut x = HashMap!{[c32], i32}::new ();
     * x:.insert ("foo", 12);
     * x:.insert ("bar", 12);
     * for k, v in x {
     *     println (k, " ", v);
     * }
     * 
     * for k in x {
     *     println (k);
     * }
     * ============
     * @returns: an iterator on the beginning of the map
     * @complexity: O (1)
     */
    pub def begin (self) -> dmut &MapIterator!{K, V} {
        if (self._loaded != 0us) {
            for i in 0us .. self._data.len {
                match self._data [i] {
                    _ : &MapValue!{K, V} =>
                        return MapIterator!{K, V}::new (i, 0us, self._data, self._data [i]);
                }
            }
        }
        
        MapIterator!{K, V}::new (0us, 0us, [], self._empty)
    } 

    /**
     * @returns: the iterator pointing to the end of the map
     * @complexity: O (1)
     */
    pub def end (self) -> &MapIterator!{K, V} {
        MapIterator!{K, V}::new (0us, 0us, [], self._empty)
    }
    
    
    /**
     * Insert an element in the hash map, without making the table grow
     * @assume: the table is able to contain the value to insert
     * @params:
     *    - h: the hash value of the key (== hash (key))
     *    - key: the key 
     *    - val: the value
     */
    prv def insertFast (mut self, h : usize, key : K, val : V) {
        let mut inserted = false;
        match ref (self._data [h % self._data.len]) {
            dmut x : &MapValue!{K, V} => {
                inserted = x:.insert (h, key, val);
            }
            _  => {
                self._data [h % self._data.len] = MapValue!{K, V}::new (h, key, val, alias self._empty);
                self._loaded += 1us;
                inserted = true;
            }
        }
        if (inserted) {
            self._size += 1us;
        }
    }


    /**
     * Make the table containing the values shrink
     * @complexity: O (n + m), with n the new size, and m the number of element contained in the old table that are reinserted
     */
    prv def shrink (mut self) -> void {
        if (self._loaded == 0us) {
            self._data = []; return {}
        }
        
        let n_len = self._data.len / 2us;
        let dmut aux = core::duplication::allocArray!{&MapNode!{K, V}} (n_len);

        for i in 0us .. aux.len {
            aux [i] = alias self._empty;
        }

        let old = self._data;
        self._data = alias aux;
        self._loaded = 0us;
        self._size = 0us;

        for i in 0us .. old.len {
            let mut current = old [i];
            loop {
                match current {
                    x : &MapValue!{K, V} => {
                        self:.insertFast (x.h, x.key, x.val);
                        current = x.next;
                    }
                    _ => {
                        break {}
                    }
                }
            }
        }
    }

    /**
     * Make the table containing the values grow
     * This function is called when the _load_factor is reached
     * If the table was empty it is allocated with a size of MapConst::DEFAULT_ALLOC_SIZE,
     * otherwise the size is mutliplied by 2
     * @complexity: O (n + m), with n the new size, and m the number of element contained in the old table that are reinserted
     */
    prv def grow (mut self) -> void {
        if (self._data.len == 0us) {
            self._data = core::duplication::allocArray!{&MapNode!{K, V}} (MapConst::DEFAULT_ALLOC_SIZE);
            for i in 0us .. self._data.len {
                self._data [i] = alias self._empty;
            }
        } else {
            let n_len = self._data.len * 2us;
            let dmut aux = core::duplication::allocArray!{&MapNode!{K, V}} (n_len);
            // Filling the data with empty elements, to be usable, allocArray is not safe!?
            for i in 0us .. aux.len {
                aux [i] = alias self._empty;
            }
            
            // Clear the map
            let old = self._data;
            self._data = alias aux;
            self._loaded = 0us;
            self._size = 0us;

            // Add all the old values 
            for i in 0us .. old.len {
                let mut current = old [i];
                loop {
                    match current {
                        x : &MapValue!{K, V} => {
                            self:.insertFast (x.h, x.key, x.val);
                            current = x.next;
                        }
                        _ => {
                            break {}
                        }
                    }
                }
            }            
        }
    }

    impl std::stream::Streamable {

        pub over toStream (self, dmut stream : &StringStream) {
            stream:.write ('{'c8);
            let mut i = 0us;
            for node in self._data {
                match node {
                    _ : &MapValue!{K, V} => {
                        if (i != 0us) { stream:.write (", "s8); }
                        stream:.write (node);
                        i = 1us;
                    }
                }
            }
            stream:.write ('}'c8);
        }
    }

    impl core::duplication::Copiable {

        /**
         * Hash map are copiable
         * Because internal values of hash map are immutable, they are not copied
         * The copy is only applied on the structure of the map
         * @example: 
         * ==========
         * let dmut a = [1, 2, 3];
         * let x = hmap#{"foo" => a};
         * 
         * let dmut y = dcopy x;
         * y:.insert ("bar", a);
         * 
         * a [0] = 89;
         * println (x); // {foo=>[89, 2, 3]}
         * println (y); // {bar=>[2, 3, 4], foo=>[89, 2, 3]}
         * ==========
         * @complexity: O(n), with n the number of elements in the map
         */
        pub over deepCopy (self) -> dmut &(Object) {
            let dmut res = HashMap!{K, V}::new (len-> self._data.len);
            
            // Add all the values 
            for i in 0us .. self._data.len {
                let mut current = self._data [i];
                loop {
                    match current {
                        x : &MapValue!{K, V} => {
                            res:.insertFast (x.h, x.key, x.val); // Insert fast, the array is already able of inserting things
                            current = x.next;
                        }
                        _ => {
                            break {}
                        }
                    }
                }
            }
            
            alias cast!{&Object} (res)
        }
        
    }
    
    
}

/**
 * The structure of the content of a hashmap is an array of simple linked list
 * The following class is the ancestor of the class that are used in the map to encode this linked list
 */    
class @abstract MapNode {K, V} {
    prot self () {}
    
    pub def opEquals (self, _ : &MapNode!{K, V})-> bool;
    
    impl std::stream::Streamable;
}

/**
 * The map value encode a node in the linked list that contains a value
 * It has a successor, and thus cannot be the final node of a linked list
 * @example: 
 * ==========
 * let list = MapValue!{i32, i32}::new (hash (12), 12, 42, 
 *                                      MapValue!{i32, i32}::new (hash (23), 23, 89, 
 *                                                                Empty!{i32, i32}::new ()
 *                                      )
 *            );
 * 
 * ==========
 */
class @final MapValue {K, V} over MapNode!{K, V} {

    /// The following node in the list
    pub let dmut next : &MapNode!{K, V};

    /// The result of hash (key), used to compare two MapValues (and avoid recomputing the value each time)
    pub let h : usize;

    /// The actual key stored in the node
    pub let key : K;

    /// The value associated with the key (it is mutable, because we can update it when inserting)
    pub let mut val : V;

    /**
     * @params:
     *    - h: the result of hash (key), to avoid recomputation
     *    - key: the key stored in the node
     *    - val: the value stored in the node
     *    - next: the reference to the next node in the list (empty or value)
     */
    pub self (h : usize, key : K, val : V, dmut next : &MapNode!{K, V})
        with next = alias next,
             key = key,
             val = val,
             h = h
    {}

    impl std::stream::Streamable {
        

        pub over toStream (self, dmut stream : &StringStream) {
            cte if (__pragma!compile ({ stream:.write (self.key); })) {
                stream:.write (self.key):.write ("=>"s8);
            } else { stream:.write (K::typeid):.write ("=>"s8); }
            
            cte if (__pragma!compile ({ stream:.write (self.val); })) {
                stream:.write (self.val);
            } else { stream:.write (V::typeid); }
            
            match self.next {
                _ : &Empty!{K, V} => {}
                _ => { stream:.write (", "s8):.write (self.next); }
            }
        }
    }

    /**
     * Insert a new element in the list, or replace an element if there is already a node whose hash value is equals to h
     * @params:
     *    - h: the hash of the key to insert
     *    - key: the key to insert
     *    - val: the value associated to the key
     * @returns: true if a new node has been allocated, false otherwise
     */
    pub def insert (mut self, h : usize, key : K, val : V) -> bool {
        cte assert (__pragma!compile ({self.key == key;}), "unusable key type : (" ~ K ~ ") must be be comparable to itself (opEquals or opCmp)");
            
        if (self.key == key) {
            self.val = val;
        } else {
            match ref self.next {
                dmut x : &MapValue!{K, V} => {
                    return x:.insert (h, key, val);
                }
                _ => {
                    self.next = MapValue::new (h, key, val, alias self.next);
                    return true;
                }
            }
        }
        false
    }

    /**
     * Find the value associated with the key whose hash is h
     * @params: 
     *    - h: the hash of the key
     * @returns: the value in an option type, or None if there is no node containing a hash equal to h
     * @complexity: O (n), with n the length of the list
     */
    pub def find (self, h : usize, key : K) -> V? {
        cte assert (__pragma!compile ({self.key == key;}), "unusable key type : (" ~ K ~ ") must be be comparable to itself (opEquals or opCmp)");

        if (self.key == key) {
            (self.val)?
        } else {
            match (self.next) {
                x : &MapValue!{K, V} => {
                    return x.find (h, key);
                }
            }
            
            (V?)::__err__
        }
    }

    
    /**
     * Remove the key inside the list, if there is no node containing the value, just return the list as it was
     * @info: the returned list is not a copy, no allocation are made
     * @complexity: O (n), with n the length of the list
     * @returns: 
     *    - rm: true if a node has been removed, false otherwise
     *    - the list, with no node containing the hash h,
     * ===========
     */
    pub def remove (mut self, h : usize, key : K, ref mut rm : bool) -> dmut &MapNode!{K, V} {
        cte assert (__pragma!compile ({self.key == key;}), "unusable key type : (" ~ K ~ ") must be be comparable to itself (opEquals or opCmp)");
            
        if (self.key == key) {
            rm = true;
            return alias self.next;
        } else {
            match ref self.next {
                dmut x : &MapValue!{K, V} => {
                    self.next = alias x:.remove (h, key, ref rm);
                }
            }
            return alias self;
        }
    }

    /**
     * Search the linked list for a node whose contained hash is h
     * @returns: true, if such a node was found, false otherwise
     * @complexity: O (n), with n the length of the list
     */
    pub def opContains (self, h : usize, key : K) -> bool {
        cte assert (__pragma!compile ({self.key == key;}), "unusable key type : (" ~ K ~ ") must be be comparable to itself (opEquals or opCmp)");
            
        if (self.key == key) {
            true
        } else {
            match self.next {
                x : &MapValue!{K, V} => {
                    return x.opContains (h, key);
                }
            }
            false
        }  
    }

    /**
     * Compare two nodes
     * They are equals, if their h is equals
     */
    pub over opEquals (self, o : &MapNode!{K, V})-> bool {
        match o {
            x : &MapValue!{K, V} =>
                return x.h == self.h;
        }
        return false;
    }
    
}

/**
 * An empty node encode the end of a list in a hash map
 * They store no values
 */
class @final Empty {K, V} over MapNode!{K, V} {
    pub self () {}

    impl std::stream::Streamable {
        /**
         * Does nothing
         */
        pub over toStream (self, dmut _ : &StringStream) {}
    }

    /**
     * Compare two nodes
     * Empty is equals to other empty nodes
     */
    pub over opEquals (self, o : &MapNode!{K, V})-> bool {
        match o {
            _ : &Empty!{K, V} =>
                return true;
        }
        return false;
    }
}

/**
 * The map iterator class is used to traverse a map
 * It can be acquired in a hash map with the methods begin (), and end ()
 * @example: 
 * =============
 * let dmut x = HashMap!{[c32], i32}::new ();
 * x:.insert ("foo", 12);
 * let dmut beg_it = alias x.begin (); 
 * assert (beg_it.get!0 == "foo");
 * assert (beg_it.get!1 == 12);
 * 
 * beg_it:.next (); // next iteration
 * assert (beg_it == x.end ()); 
 * =============
 */
pub class @final MapIterator {K, V} {

    /// A reference on the table contained in the map we are traversing
    prv let mut _data : [&MapNode!{K, V}];

    /// The current index of the iterator, in the table
    prv let mut _index : usize;

    /// The current index in the current branch (linked list of MapValues)
    prv let mut _branch : usize = 0us;

    /// The reference on node containing the value we are pointing at
    prv let mut _currentNode : &MapNode!{K, V};

    /**
     * An iterator is always constructed to point somewhere, or must point to (0, 0, [], empty)
     * @params: 
     *    - i: the index in the table of this iterator
     *    - branch: the index in the branch at iterator i
     *    - data: the table of data
     *    - current: the current traversed node in the branch (data [i][branch])
     */
    pub self (i : usize, branch : usize, data : [&MapNode!{K, V}], current : &MapNode!{K, V})
        with _data = data,
             _index = i,
             _branch = branch,
             _currentNode = current
    {}        

    /**
     * Two iterators are equals, if the current nodes are equals
     */
    pub def opEquals (self, o : &MapIterator!{K, V}) -> bool {
        self._currentNode == o._currentNode
    }

    /**
     * @returns: 
     * =======
     * the key of the current node, in an option type
     * None, it the current node is empty
     * =======     
     */
    pub def get {0} (self) -> K {
        match (self._currentNode) {
            x : &MapValue!{K, V} =>
                return x.key;
        }
        
        __pragma!panic ();
    }

    /**
     * @returns: 
     * =======
     * the value of the current node, in an option type
     * None, it the current node is empty
     * =======     
     */
    pub def get {1} (self) -> V {
        match (self._currentNode) {
            x : &MapValue!{K, V} =>
                return x.val;
        }
        
        __pragma!panic ();
    }

    /**
     * Create a new iterator that point to the next value contained in the map
     * If there is no more value, the returned iterator is equals to map.end ()
     * @example: 
     * ===========
     * let dmut x = HashMap!{[c32], i32}::new ();
     * x:.insert ("foo", 12);
     * x:.insert ("bar", 12);
     * let dmut it = alias x.begin ();
     * let end = x.end (); // put in a var to avoid multiple useless calls and allocations
     * while (it != end) {
     *    println (it.get!0, " ", it.get!1);
     *    it:.next ();
     * }
     * ===========
     */
    pub def next (mut self) -> void {
        match (self._currentNode) {
            x : &MapValue!{K, V} => {
                match (x.next) {
                    _ : &MapValue!{K, V} => {
                        self._branch += 1us;
                        self._currentNode = x.next;
                        return {}
                    }
                }
            }
        }            

        if (self._index + 1us < self._data.len) {
            for j in (self._index + 1us).. self._data.len {
                match (self._data [j]) {
                    _ : &MapValue!{K, V} => {
                        self._branch = 0us;
                        self._index = j;
                        self._currentNode = self._data [j];
                        return {};
                    }
                }
            }
        }

        self._data = [];
        self._currentNode = Empty!{K, V}::new ();
        self._index = 0us;
        self._branch = 0us;
    }
    
}
