/**
 * This module implement the exception SyntaxError.
 * @Authors: Emile Cadorel
 * @License: GPLv3
 */

mod std::syntax::errors;

import core::exception;
import std::stream;

/**
 * Error thrown when a syntax error is encountered. 
 */
pub class @final SyntaxError over Exception {

    // The message of the error
    pub let msg : [c32];

    // The line location of the error
    pub let line : u64;

    // The column location of the error
    pub let col : u64;

    /**
     * @params: 
     *     - msg: a message to clarify the error
     *     - line: the line location of the error
     *     - col: the column location of the error
     */
    pub self (msg : [c32], line : u64, col : u64)
        with msg = msg, line = line, col = col
    {}

    impl Streamable {
        
        pub over toStream (self, dmut stream : &StringStream) {            
            stream:.write ("SyntaxError ("s8, self.line, ","s8, self.col, ") : ", self.msg);
            
            if (self.trace.len != 0u64) {
                stream:.write (": "s8):.write (self.getStackTrace ());               
            }
        }
    }        
}
