in list;

use std::stream;
use core::types::array;

pub record FuncStack {

    let mut _len : usize = 0us;
    
    let dmut _data : [[u32]] = [];    

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ======================================          CTOR          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */
    
    pub self () {}

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =====================================          USAGE          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Insert an element at the top of the stack
     * 
     * */
    pub fn push (mut self, dmut lst : [u32]) {
        if self._data.len == self._len {
            self:.grow ();
        }

        self._data [self._len] = alias lst;
        self._len += 1;
    }

    /**
     * @returns: the element at the top of the task
     * */
    pub fn top (mut self)-> dmut [u32] {
        if self._len > 0 {
            return alias self._data [self._len - 1]
        }

        []
    }

    /**
     * Remove the element at the top of the stack (does nothing if empty)
     * */
    pub fn pop (mut self)-> dmut [u32] {
        if self._len == 0 { return []; }
        let dmut result = self:.top (); 
        
        self._len -= 1;
        alias result
    }

    /**
     * @returns: true iif the stack is empty
     * */
    pub fn isEmpty (self)-> bool {
        self._len == 0
    }
    
    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          PRIVATE          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */
    
    /**
     * Change the size of the capacity to write other elements
     * @complexity: O (n), with n = self._len
     */
    prv fn grow (mut self) {
        let addLen = if (self._data.len == 0us) {
            2us
        } else {
            self._data.len 
        }
            
        self._data ~= copy [[] ; addLen];                                             
    }

}
