mod core::exception;
import core::typeinfo;
import std::stream;

extern (C) def _yrt_exc_get_stack_trace ()-> [&(void)];
extern (C) def _yrt_exc_resolve_stack_trace (trc : [&void])-> [c8];

pub aka _yrt_jmp_buf_type = [u8 ; 200];


pub class @abstract Exception {

    let next : &(void) = null;

    let trace : [&(void)];

    self () with trace = _yrt_exc_get_stack_trace () {}

    impl stream::Streamable {
        
        prot over __stream__streamContent (self, dmut _ : &StringStream) -> bool {
            false            
        }

        pub over toStream (self, dmut stream : &StringStream)-> void {
            stream:.write (self::typeinfo.name):.write(" ("s8);
            self.__stream__streamContent (alias stream);
            stream:.write (')'c8);
            if (self.trace.len != 0us) {
                stream:.write (":\n"s8);
                stream:.write (self.getStackTrace ());
            }
        }
        
    }

    prot def hasTrace (self)-> bool {
        self.trace.len != 0us
    }
    
    pub def @final getStackTrace (self) -> [c8] {
        _yrt_exc_resolve_stack_trace (self.trace)
    }

}

pub class AssertError over Exception {
    pub let msg : [c32];

    pub self (msg : [c32]) with msg = msg {}

    impl stream::Streamable {
        pub over toStream (self, dmut stream : &StringStream) {
            self::super.toStream (alias stream);
        }
    }
}

pub class RuntimeError over Exception {
    pub let msg : [c8];

    pub self (msg : [c8]) with msg = msg {}

    impl stream::Streamable {
        pub over toStream (self, dmut stream : &StringStream) {
            self::super.toStream (alias stream);
        }
    }
}

pub class SegFault over Exception {
    pub self () {}

    impl stream::Streamable {
        pub over toStream (self, dmut stream : &StringStream) {
            self::super.toStream (alias stream);
        }
    }
}

pub def abort (test : bool, msg : [c32])
    throws &AssertError
{
    if (!test)
        throw AssertError::new (msg)
}

pub def abort (test : bool)
    throws &AssertError
{
    if (!test)
        throw AssertError::new ("")
}

pub extern (C) def _yrt_throw_seg_fault () -> void
    throws &SegFault
{
    throw SegFault::new ()
}

pub extern (C) def _yrt_throw_runtime_abort (c : [c8]) -> void
    throws &RuntimeError
{
    throw RuntimeError::new (c);
}
