mod core::array;
import core::exception, core::typeinfo;
import std::io, std::stream;

mod allocMod {T} {
    pub extern (C) def _yrt_new_array (_ : usize, _ : u64)-> mut [mut T];
}

pub class OutOfArray over Exception {
    pub self () {}

    impl stream::Streamable {
        pub over toStream (self, dmut stream : &StringStream) {
            self::super.toStream (alias stream);
        }
    }
}

pub def outOfArray ()
    throws &OutOfArray
{
    throw OutOfArray::new ();
}


pub def opCmp {T of [U], U} (a : T, b : T)-> i32 {
    let dmut len = if (b.len < a.len) {b.len} else {a.len};
    for i in 0us .. len {
        if (a [i] < b[i]) return -1;
        else if (a[i] > b[i]) return 1;
    }
    
    cast!(i32) (a.len - b.len)
}

pub def opEquals {T of [U], U} (a : T, b : T)-> bool {
    if (a.len != b.len) return false;
    for i in 0us .. a.len {
        if a [i] != b [i] { return false; }
    }

    true
}

pub macro str {
    pub self (fst=(__char)*) {
        "#(fst)"
    }
}

pub macro str8 {
    pub self (fst=(__char)*) {
        "#(fst)"s8
    }
}
