/**
 * This module implements the management of reflection for symbol
 * retreiving using their string name. Symbols are stored in a HashMap
 * once loaded (from ELF on Linux for example), so that accessing them
 * is not that costly. The creation of the index is relatively
 * expensive on the other hand, but is done only one time for every
 * loaded segments (executable, so libs on linux, etc.).
 * 
 * @Authors: Emile Cadorel
 * @License: GPLv3
 */

mod core::reflect;

import std::collection::map;
import std::conv;
import std::io;

pub enum
| FUNCTION = 0
| VTABLE   = 1
| NONE	   = 2
 -> ReflectSymbolType;

pub struct
| type : ReflectSymbolType
| ptr : usize
| size : usize
| name : &c8
 -> ReflectSymbol;

extern (C) static dmut __YRT_REFLECT_SYMBOL_TABLE__ : &(&HashMap!{[c8], ReflectSymbol});
extern (C) static dmut __YRT_INV_REFLECT_SYMBOL_TABLE__ : &(&HashMap!{usize, ReflectSymbol});

__version WINDOWS {
    struct
    | entries : [ReflectSymbolTableEntry]
     -> ReflectSymbolTable;

    struct
    | moduleName : &c8
    | symbols : [ReflectSymbol]
     -> ReflectSymbolTableEntry;
    
    extern (C) def _yrt_reflect_construct_index_table (symbol_table : ReflectSymbolTable) {
        for entry in symbol_table.entries {
            for sym in entry.symbols {
                let name = sym.name.fromStringZ ()?;
                match name {
                    Ok (s : _) => 
                        __REFLECT_SYMBOL_TABLE__:.insert (s, sym);
                }
                
                __INV_REFLECT_SYMBOL_TABLE__:.insert (sym.ptr, sym);
            }
        }
    }
}

__version LINUX {

    import etc::elf;
    
    extern (C) static dmut __YRT_ELF_LOADER__ : &(&ELFLoader);

    extern (C) def _yrt_reflect_update_index_table () {
        if (__YRT_ELF_LOADER__ is null) {
            __YRT_ELF_LOADER__ = duplication::alloc (alias ELFLoader::new ());
        }
        
        __pragma!trusted ((*__YRT_ELF_LOADER__):.update ());
    }

    extern (C) def _yrt_reflect_update_index_table_with_elf_name (name : [c8]) {
        if (__YRT_ELF_LOADER__ is null) {
            __YRT_ELF_LOADER__ = duplication::alloc (alias ELFLoader::new ());
        }
        
        __pragma!trusted ((*__YRT_ELF_LOADER__):.update (name));
    }
    
    pub extern (C) def _yrt_reflect_register_symbol (sym : ReflectSymbol) {
        if (__YRT_REFLECT_SYMBOL_TABLE__ is null) {
            __YRT_REFLECT_SYMBOL_TABLE__ = duplication::alloc (alias HashMap!{[c8], ReflectSymbol}::new ());
            __YRT_INV_REFLECT_SYMBOL_TABLE__ = duplication::alloc (alias HashMap!{usize, ReflectSymbol}::new ());
        }
        
        let name = sym.name.fromStringZ ()?;
        match name {
            Ok (s : _) => {
                __pragma!trusted ((*__YRT_REFLECT_SYMBOL_TABLE__):.insert (s, sym));
            }
        }
        
        __pragma!trusted ((*__YRT_INV_REFLECT_SYMBOL_TABLE__):.insert (sym.ptr, sym));        
    }
    
}

extern (C) def _yrt_reflect_find_symbol_in_indexed_table (name : [c8])-> ReflectSymbol {
    if (__YRT_REFLECT_SYMBOL_TABLE__ is null) {
        return ReflectSymbol (ReflectSymbolType::NONE, 0us, 0us, null);
    }
    
    match (__pragma!trusted ((*__YRT_REFLECT_SYMBOL_TABLE__))).find (name) {
        Ok (sym : _) => {
            return sym
        }
        _ => {
            return ReflectSymbol (ReflectSymbolType::NONE, 0us, 0us, null);
        }
    }
}

extern (C) def _yrt_reflect_find_symbol_in_indexed_table_from_addr (addr : usize)-> ReflectSymbol {
    if (__YRT_REFLECT_SYMBOL_TABLE__ is null) {
        return ReflectSymbol (ReflectSymbolType::NONE, 0us, 0us, null);
    }
    
    let mut min = usize::max;
    let mut symbol = ReflectSymbol (ReflectSymbolType::NONE, 0us, 0us, null);
    for _, sym in __pragma!trusted ((*__YRT_REFLECT_SYMBOL_TABLE__)) {
        if (sym.ptr < addr && addr < (sym.ptr + sym.size)  && (addr - sym.ptr) < min) {
            symbol = sym;
            min = (addr - sym.ptr);
        } else if (sym.ptr == addr) {
            return sym;
        }
    }

    return symbol
}
